plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
    id 'signing'
}

group 'com.github.jongwook'
version '0.0.5-SNAPSHOT'

def jacksonVersion = '2.7.4'
def asynchbaseVersion = '1.7.2-SNAPSHOT'
def curatorVersion = '2.11.0'
def guavaVersion = '18.0'
def ningVersion = '1.9.29'
def titanVersion = '1.0.0'

sourceCompatibility = 1.7

ext.isReleaseVersion = !project.version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: jacksonVersion
    compile(group: 'com.github.jongwook', name: 'asynchbase', version: asynchbaseVersion) {
        exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
    }
    compile group: 'org.apache.curator', name: 'curator-framework', version: curatorVersion
    compile group: 'com.thinkaurelius.titan', name: 'titan-hbase', version: titanVersion
    compile group: 'com.thinkaurelius.titan', name: 'titan-berkeleyje', version: titanVersion
    compile group: 'com.google.guava', name: 'guava', version: guavaVersion
    compile group: 'com.ning', name: 'async-http-client', version: ningVersion
}

shadowJar {
    relocate 'com.fasterxml.jackson', 'com.kakao.shaded.jackson'
    relocate 'com.google.common', 'com.kakao.shaded.guava'
    relocate 'com.ning', 'com.kakao.shaded.ning'
    relocate 'com.thoughtworks.paranamer', 'com.kakao.shaded.paranamer'
    relocate 'org.jboss.netty', 'com.kakao.shaded.netty'

    baseName = "shades"
    classifier = ''
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            url "https://oss.sonatype.org/${ isReleaseVersion ? '/service/local/staging/deploy/maven2' : 'content/repositories/snapshots' }"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }

    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifactId = "shades"

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

signing {
    sign shadowJar
    sign sourceJar
    sign javadocJar
}
